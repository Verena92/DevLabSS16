package de.hdm.wim.events;

import java.util.ArrayList;
import java.util.List;

import de.hdm.wim.events.EventService;
import de.hdm.wim.events.model.User;

import de.hdm.wim.events.model.InternalToken;
import de.hdm.wim.events.model.KeywordInformation;
import de.hdm.wim.events.model.Project;
import de.hdm.wim.events.model.Company;
import de.hdm.wim.events.model.Employee;
import de.hdm.wim.events.model.Document;
import de.hdm.wim.events.model.DocumentForSpeechTokenizer;
import de.hdm.wim.events.model.ProjectReceivedEvent;
import de.hdm.wim.events.model.CompanyReceivedEvent;
import de.hdm.wim.events.model.EmployeeReceivedEvent;
import de.hdm.wim.events.model.DocumentReceivedEvent;
import de.hdm.wim.events.model.DocumentSuggestionReactionEvent;

import de.hdm.wim.events.documentrepresentation.DocumentRepresentationRequester;
import de.hdm.wim.events.documentrepresentation.SearchRequest;
import de.hdm.wim.events.documentrepresentation.DocumentIDsWrapper;
import de.hdm.wim.events.speechtokenizer.SpeechTokenSender;

global java.util.List resultList;

/////////////////////////////////////////////////////
////////////// Event Declarations ///////////////////
/////////////////////////////////////////////////////

declare InternalToken
    @role( event )
    @timestamp( timestamp.getTime() ) //long
end

declare DocumentSuggestionReactionEvent
    @role( event )
    @timestamp( timestamp.getTime() ) //long
end

declare ProjectReceivedEvent
    @role( event )
    @timestamp( timestamp.getTime() ) //long
end

declare CompanyReceivedEvent
    @role( event )
    @timestamp( timestamp.getTime() ) //long
end

declare EmployeeReceivedEvent
    @role( event )
    @timestamp( timestamp.getTime() ) //long
end

declare DocumentReceivedEvent
    @role( event )
    @timestamp( timestamp.getTime() ) //long
end


//////////////////////////////////////////////////////
///////////// SpeechToken -> Events //////////////////
//////////////////////////////////////////////////////
rule "A Token occured" dialect "mvel"
  when
      $t : InternalToken() from entry-point SpeechTokenEventStream
  then
  	  resultList.add( "A Token occured");
  	  System.out.println( "A Token occured"); 
end

rule "Two Tokens occured within 10 seconds" dialect "mvel"
  when
      $t : InternalToken() from entry-point SpeechTokenEventStream
      InternalToken( $t.id != id, this after[0s,10s] $t) from entry-point SpeechTokenEventStream      
  then
  	  resultList.add( "Two Tokens occured within 10 seconds");
  	  System.out.println( "Two Tokens occured within 10 seconds"); 
end

rule "A Token with exactly 1 related project occured"
    when
        $t : InternalToken( keywordInformation.projects.size() == 1, 
        			keywordInformation.companies.empty,
        			keywordInformation.employees.empty) from entry-point SpeechTokenEventStream 
    then
      resultList.add( "A Token with exactly 1 related project occured");
      System.out.println( "A Token with exactly 1 related project occured");
      
      DocumentRepresentationRequester requester = new DocumentRepresentationRequester();
      Project project = requester.getProject($t.getKeywordInformation().getProjects().get(0));
      System.out.println( "requested project: " + project);
      ProjectReceivedEvent projectReceivedEvent = new ProjectReceivedEvent( project);
      EventService.merge(projectReceivedEvent);
      insert( projectReceivedEvent );
end

rule "A Token with exactly 1 related company occured"
    when
        $t : InternalToken( keywordInformation.projects.empty, 
        			keywordInformation.companies.size() == 1,
        			keywordInformation.employees.empty) from entry-point SpeechTokenEventStream 
    then
      resultList.add( "A Token with exactly 1 related company occured");
      System.out.println( "A Token with exactly 1 related company occured"); 
      
      DocumentRepresentationRequester requester = new DocumentRepresentationRequester();
      Company company = requester.getCompany($t.getKeywordInformation().getCompanies().get(0));
      System.out.println( "requested company: " + company);
      CompanyReceivedEvent companyReceivedEvent = new CompanyReceivedEvent( company);
      EventService.merge(companyReceivedEvent);
      insert( companyReceivedEvent );
end

rule "A Token with exactly 1 related employee occured"
    when
        $t : InternalToken( keywordInformation.projects.empty, 
        			keywordInformation.companies.empty,
        			keywordInformation.employees.size() == 1) from entry-point SpeechTokenEventStream 
    then
      resultList.add( "A Token with exactly 1 related employee occured");
      System.out.println( "A Token with exactly 1 related employee occured");
      
      DocumentRepresentationRequester requester = new DocumentRepresentationRequester();
      Employee employee = requester.getEmployee($t.getKeywordInformation().getEmployees().get(0));
      System.out.println( "requested employee: " + employee);
      
      EmployeeReceivedEvent employeeReceivedEvent = new EmployeeReceivedEvent( employee);
      EventService.merge(employeeReceivedEvent);
      insert( employeeReceivedEvent );
end


rule "A document suggestion was accepted"
    when
    	$dsre : DocumentSuggestionReactionEvent(  accepted == true) from entry-point SpeechTokenEventStream 
    then
      resultList.add( "A DocumentSuggestion was accepted");
      System.out.println( "A DocumentSuggestion was accepted");
end

rule "A document suggestion was declined"
    when
    	$dsre : DocumentSuggestionReactionEvent(  accepted == false) from entry-point SpeechTokenEventStream 
    then
      resultList.add( "A DocumentSuggestion was declined");
      System.out.println( "A DocumentSuggestion was declined");
end

//////////////////////////////////////////////////////
////////// Events -> DocumentRepresentation //////////
//////////////////////////////////////////////////////

rule "Request a document for a project"
    when
      $pre : ProjectReceivedEvent();
    then
      resultList.add( "Request a document for a project");
      
      DocumentRepresentationRequester requester = new DocumentRepresentationRequester();
	  List<String> employees = new ArrayList<String>();
	  List<String> companies = new ArrayList<String>();
	  List<String> projects = new ArrayList<String>();
	  projects.add( $pre.getProject().getProjectID());
	  SearchRequest searchRequest = new SearchRequest(employees, companies, projects, "");
	  DocumentIDsWrapper wrapper =  requester.getDocumentIDs(searchRequest);
	  
	  String documentID = wrapper.getDocuments().get(0);  //TODO: ggf. bekommen wir hier mehr als 1 Document zurück!
	  if( documentID != null) {
	  	Document document = requester.getDocument(documentID);
	  	System.out.println( "found document: " + document);
        DocumentReceivedEvent documentReceivedEvent = new DocumentReceivedEvent( document);
        EventService.merge(documentReceivedEvent);
        insert( documentReceivedEvent );
	  }
end

rule "Request a document for a company"
    when
      $cre : CompanyReceivedEvent();
    then
      resultList.add( "Request a document for a company");
      
      DocumentRepresentationRequester requester = new DocumentRepresentationRequester();
	  List<String> employees = new ArrayList<String>();
	  List<String> companies = new ArrayList<String>();
	  companies.add($cre.getCompany().getCompanyID());
	  List<String> projects = new ArrayList<String>();
	  SearchRequest searchRequest = new SearchRequest(employees, companies, projects, "");
	  DocumentIDsWrapper wrapper =  requester.getDocumentIDs(searchRequest);
	  
	  String documentID = wrapper.getDocuments().get(0);  //TODO: ggf. bekommen wir hier mehr als 1 Document zurück!
	  if( documentID != null) {
	  	Document document = requester.getDocument(documentID);
	  	System.out.println( "found document: " + document);
        DocumentReceivedEvent documentReceivedEvent = new DocumentReceivedEvent( document);
        EventService.merge(documentReceivedEvent);
        insert( documentReceivedEvent );
	  }
end

rule "Request a document for an employee"
    when
      $ere : EmployeeReceivedEvent();
    then
      resultList.add( "Request a document for an employee");
      
      DocumentRepresentationRequester requester = new DocumentRepresentationRequester();
	  List<String> employees = new ArrayList<String>();
	  employees.add($ere.getEmployee().getEmployeeID());
	  List<String> companies = new ArrayList<String>();
	  List<String> projects = new ArrayList<String>();
	  SearchRequest searchRequest = new SearchRequest(employees, companies, projects, "");
	  DocumentIDsWrapper wrapper =  requester.getDocumentIDs(searchRequest);
	  
	  String documentID = wrapper.getDocuments().get(0);  //TODO: ggf. bekommen wir hier mehr als 1 Document zurück!
	  if( documentID != null) {
	  	Document document = requester.getDocument(documentID);
	  	System.out.println( "found document: " + document);
        DocumentReceivedEvent documentReceivedEvent = new DocumentReceivedEvent( document);
        EventService.merge(documentReceivedEvent);
        insert( documentReceivedEvent );
	  }
end

//////////////////////////////////////////////////////
////////////// Events -> SpeechToken /////////////////
//////////////////////////////////////////////////////

rule "send Document to SpeechToken"
    when
      $dre : DocumentReceivedEvent();
    then
      resultList.add( "send Document to SpeechToken");
      SpeechTokenSender sender = new SpeechTokenSender();
      
      List<User> activeUsers = EventService.getActiveUsers(); //doesn't work in unit tests.
      for( User user: activeUsers) {
        DocumentForSpeechTokenizer documentForSpeechTokenizer = new DocumentForSpeechTokenizer(user, $dre.getDocument()); 
        sender.sendDocument( documentForSpeechTokenizer); 
	  }      
end

